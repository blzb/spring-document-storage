/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'blzb' at '10/8/15 9:08 PM' with Gradle 2.6
 *
 * This generated file contains a sample Groovy project to get you started.
 * For more details take a look at the Groovy Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.6/userguide/tutorial_groovy_projects.html
 */
import java.text.SimpleDateFormat


buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'

  }
}

plugins {
  id 'net.researchgate.release' version '2.3.0'
}
// Apply the groovy plugin to add support for Groovy
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.ben-manes.versions'

group = project.groupId
sourceCompatibility = project.javaVersion
targetCompatibility = project.javaVersion
version = project.version

// In this section you declare where to find the dependencies of your project
repositories {
  // Use 'jcenter' for resolving your dependencies.
  // You can declare any Maven/Ivy/file repository here.
  jcenter()
  mavenLocal()
}

// In this section you declare the dependencies for your production and test code
dependencies {
  // We use the latest groovy 2.x version for building this library
  compile 'org.codehaus.groovy:groovy-all:2.4.4'
  compile 'org.springframework.boot:spring-boot-starter-jdbc:1.2.6.RELEASE'
  compile 'org.apache.tika:tika-core:1.10'
  compile 'org.apache.tika:tika-parsers:1.10'
  compile 'org.thymeleaf:thymeleaf-spring4:2.1.4.RELEASE'

// We use the awesome Spock testing and specification framework
  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  testCompile 'com.h2database:h2:1.4.190'

}

jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
    html.enabled = true
  }
}


Date buildTimeAndDate = new Date()
ext {
  buildDate = new SimpleDateFormat('dd-MMM-yyyy').format(buildTimeAndDate)
  buildTime = new SimpleDateFormat('hh:mm aa').format(buildTimeAndDate)
}

def jarManifestAttributes = [
  'Built-By'  : System.properties['user.name'],
  'Created-By': System.properties['java.version'] + ' (' + System.properties['java.vendor'] + ' ' + System.getProperty("java.vm.version") + ")",
  'Build-Date': buildDate,
  'Build-Time': buildTime]

jar {
  manifest {
    attributes(jarManifestAttributes)
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar

      pom.withXml {
        def root = asNode()
        root.appendNode('name', project.name)
        root.appendNode('description', project.description)
        root.appendNode('url', 'https://github.com/blzb/spring-document-storage')
        root.appendNode('inceptionYear', '2015')

        def scm = root.appendNode('scm')
        scm.appendNode('url', 'https://github.com/blzb/spring-document-storage')
        scm.appendNode('connection', 'scm:https://blzb@github.com/blzb/spring-document-storage.git')
        scm.appendNode('developerConnection', 'scm:git@github.com:blzb/spring-document-storage.git')

        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode('name', 'The Apache Software License, Version 2.0')
        license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
        license.appendNode('distribution', 'repo')

        def developers = root.appendNode('developers')
        def domix = developers.appendNode('developer')
        domix.appendNode('id', 'blzb')
        domix.appendNode('name', 'Angel Pimentel')
        domix.appendNode('email', 'angelpmza@gmail.com')
      }
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUsername') ? bintrayUsername : ''
  key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''

  publications = ['mavenJava']
  dryRun = false //Whether to run this as dry-run, without deploying
  publish = true //If version should be auto published after an upload
  pkg {
    repo = 'oss'
    userOrg = 'blzb'
    name = project.name
    desc = project.description
    websiteUrl = 'https://github.com/blzb/spring-document-storage'
    issueTrackerUrl = 'https://github.com/blzb/spring-document-storage/issues'
    vcsUrl = 'https://github.com/blzb/spring-document-storage.git'
    licenses = ['Apache-2.0']
    labels = ['groovy', 'document-storage', 'spring']
    publicDownloadNumbers = true
    attributes = [:]
    //Optional version descriptor
    version {
      name = project.version
      desc = project.description
      mavenCentralSync {
        sync = false
        user = ''
        password = ''
        close = '1'
      }
    }
  }
}
createReleaseTag.dependsOn bintrayUpload